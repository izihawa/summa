// doi = @{ "doi.org/"? ~ "10." ~ ASCII_DIGIT{4,9} ~ WHITE_SPACE? ~ "/" ~ WHITE_SPACE? ~ ("-" | "." | "_" | ";" | "(" | ")" | "<" | ">" | "/" | ":" | ASCII)+ }

quote = _{ "\"" | "'" | "`" | "'" | "“" | "”" | "‘" | "«" | "»" | "„" | "`"}
expression_sep = _{ WHITE_SPACE | "," | "." | ";" | "!" | "?" | (":" ~ WHITE_SPACE)}

star = { "*" }
slop = @{ DECIMAL_NUMBER+ }
boost = { (DECIMAL_NUMBER | ".")+ }
range = ${ "[" ~ WHITE_SPACE* ~ boundary_word ~ WHITE_SPACE+ ~ ^"to" ~ WHITE_SPACE+ ~ boundary_word ~ WHITE_SPACE* ~ "]" }

field_name = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
word = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" | "+" | "#" | "-")* }
boundary_word = _{ word | star }
words = @{ word ~ (WHITE_SPACE+ ~ word)* }
phrase = ${ quote ~ words ~ quote ~ ("~" ~ slop)? }
literal = { phrase | word }
search_group = ${ field_name ~ ":" ~ (range | literal) }

statement = ${ (search_group | literal) ~ ("^" ~ boost)? }

positive_expression = ${ "+" ~ statement }
negative_expression = ${ "-" ~ statement }
default_expression = ${ statement }

expression = _{ positive_expression | negative_expression | default_expression }

expressions = _{ expression ~ (expression_sep+ ~ expression)* ~ expression_sep? }
main = _{SOI ~ expressions}
