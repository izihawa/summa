quote = _{ "\"" | "'" | "`" | "'" | "“" | "”" | "‘" | "«" | "»" | "„" | "`"}
star = { "*" }
statement_sep = _{ WHITE_SPACE | "," | "." | ";" | "!" | "?" | (":" ~ WHITE_SPACE) | "(" | ")" | "/" }
allowed_chars = _{"_" | "+" | "#" | "-"}

isbn = @{ "978" ~ ("-"? ~ ASCII_DIGIT){7,10} ~ EOI }
doi = @{ "10." ~ ASCII_DIGIT{4,9} ~ WHITE_SPACE? ~ "/" ~ WHITE_SPACE? ~ (!WHITE_SPACE ~ ANY)+ }
wrapped_doi = _{ ("http" ~ "s"? ~ "://")? ~ "doi.org/"? ~ doi }

slop = @{ DECIMAL_NUMBER+ }
boost = { (DECIMAL_NUMBER | ".")+ }
range = ${ "[" ~ WHITE_SPACE* ~ boundary_word ~ WHITE_SPACE+ ~ ^"to" ~ WHITE_SPACE+ ~ boundary_word ~ WHITE_SPACE* ~ "]" }

field_name = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" | ".")* }
word = @{ wrapped_doi | ((LETTER | NUMBER) ~ (LETTER | NUMBER | "_" | "+" | "#" | "-" | "–")*) }

boundary_word = _{ star | word }
phrase_internals = { (!quote ~ ANY)* }
phrase = ${ quote ~ phrase_internals? ~ (quote | EOI) ~ ("~" ~ slop)? }
regex_internals = ${ (!PEEK ~ ANY)* }
regex = ${ PUSH("/"+) ~ regex_internals ~ POP }

pre_term = _{ phrase | regex | range | word }

positive_term = ${ "+" ~ pre_term }
negative_term = ${ "-" ~ pre_term }
default_term = ${ pre_term }

term = { positive_term | negative_term | default_term }
grouping = { "(" ~ WHITE_SPACE? ~ term ~ (WHITE_SPACE+ ~ term)* ~ WHITE_SPACE? ~ ")" }
search_group = ${ field_name ~ ":" ~ (grouping | term) }

statement = ${ (isbn | wrapped_doi | search_group | term) ~ ("^" ~ boost)? }
statements = _{ statement_sep* ~ statement? ~ (statement_sep+ ~ statement)* ~ statement_sep* }
main = _{SOI ~ statements}
